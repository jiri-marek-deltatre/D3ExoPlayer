// Copyright (C) 2017 The Android Open Source Project
//
// Licensed under the Apache License, Version 2.0 (the "License");
// you may not use this file except in compliance with the License.
// You may obtain a copy of the License at
//
//      http://www.apache.org/licenses/LICENSE-2.0
//
// Unless required by applicable law or agreed to in writing, software
// distributed under the License is distributed on an "AS IS" BASIS,
// WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
// See the License for the specific language governing permissions and
// limitations under the License.

apply plugin: 'maven-publish'
apply from: "$gradle.ext.exoplayerSettingsDir/missing_aar_type_workaround.gradle"

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release
                artifact androidSourcesJar
                groupId 'com.deltatre.android.exoplayer'
                artifactId = releaseArtifactId
                version '2.18.7-d3'

                pom {
                    name = releaseArtifactId
                    description = releaseDescription
                    licenses {
                        license {
                            name = 'The Apache Software License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                            distribution = 'repo'
                        }
                    }
                    developers {
                        developer {
                            name = 'The Android Open Source Project'
                        }
                    }
                    scm {
                        connection = 'scm:git:https://github.com/jiri-marek-deltatre/D3ExoPlayer.git'
                        url = 'https://github.com/jiri-marek-deltatre/D3ExoPlayer'
                    }
                    withXml {
                        addMissingAarTypeToXml(it)
                    }
                }
            }

        }
        repositories {
            mavenLocal()
            maven {
                name "Github"
                url "https://maven.pkg.github.com/jiri-marek-deltatre/D3ExoPlayer"
                credentials {
                    //you need to have global file gradle.properties in your /.gradle/ directory and put there
                    //gpr_user = yourGithubUsername
                    //gpr_key = yourGithubToken
                    //PS: do not put there any ' or "
                    username = project.findProperty("gitUsername") ?: System.getenv("EXOPLAYER_REPOSITORY_USERNAME")
                    password = project.findProperty("gitPassword") ?: System.getenv("EXOPLAYER_REPOSITORY_TOKEN")
                }
            }
        }
    }
}

task androidSourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    from android.sourceSets.main.java.srcDirs
}
